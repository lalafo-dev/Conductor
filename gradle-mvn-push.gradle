/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'

def isReleaseBuild() {
  return VERSION_NAME.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
  if (System.getenv().containsKey("RELEASE_REPOSITORY_URL")) {
    return System.getenv().get("RELEASE_REPOSITORY_URL")
  }
  return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL :
      "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
  if (System.getenv().containsKey("SNAPSHOT_REPOSITORY_URL")) {
    return System.getenv().get("SNAPSHOT_REPOSITORY_URL")
  }
  return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL :
          "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
  if (System.getenv().containsKey("NEXUS_USERNAME")) {
    return System.getenv().get("NEXUS_USERNAME")
  }
  return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
  if (System.getenv().containsKey("NEXUS_PASSWORD")) {
    return System.getenv().get("NEXUS_PASSWORD")
  }
  return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

def getSnapshotRepositoryUsername() {
  if (System.getenv().containsKey("SNAPSHOT_NEXUS_USERNAME")) {
    return System.getenv().get("SNAPSHOT_NEXUS_USERNAME")
  }
  return hasProperty('SNAPSHOT_NEXUS_USERNAME') ? SNAPSHOT_NEXUS_USERNAME : ""
}

def getSnapshotRepositoryPassword() {
  if (System.getenv().containsKey("SNAPSHOT_NEXUS_PASSWORD")) {
    return System.getenv().get("SNAPSHOT_NEXUS_PASSWORD")
  }
  return hasProperty('SNAPSHOT_NEXUS_PASSWORD') ? SNAPSHOT_NEXUS_PASSWORD : ""
}

afterEvaluate { project ->
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        pom.groupId = GROUP
        pom.artifactId = POM_ARTIFACT_ID
        pom.version = VERSION_NAME

        repository(url: getReleaseRepositoryUrl()) {
          authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
        }
        snapshotRepository(url: getSnapshotRepositoryUrl()) {
          authentication(userName: getSnapshotRepositoryUsername(), password: getSnapshotRepositoryPassword())
        }

        pom.project {
          name POM_NAME
          packaging POM_PACKAGING
          description POM_DESCRIPTION
          url POM_URL

          scm {
            url POM_SCM_URL
            connection POM_SCM_CONNECTION
            developerConnection POM_SCM_DEV_CONNECTION
          }

          licenses {
            license {
              name POM_LICENCE_NAME
              url POM_LICENCE_URL
              distribution POM_LICENCE_DIST
            }
          }

          developers {
            developer {
              id POM_DEVELOPER_ID
              name POM_DEVELOPER_NAME
            }
          }
        }

        // Resolve dependencies to other modules
        pom.whenConfigured { pom ->
          pom.dependencies.findAll { dep -> dep.groupId == rootProject.name }.collect { dep ->
            dep.groupId = pom.groupId = project.GROUP
            dep.version = pom.version = project.VERSION_NAME
          }
        }
      }
    }
  }

  signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
  }

  if (project.getPlugins().hasPlugin('com.android.application') ||
          project.getPlugins().hasPlugin('com.android.library')) {
    task install(type: Upload, dependsOn: assemble) {
      repositories.mavenInstaller {
        configuration = configurations.archives

        pom.groupId = GROUP
        pom.artifactId = POM_ARTIFACT_ID
        pom.version = VERSION_NAME

        pom.project {
          name POM_NAME
          packaging POM_PACKAGING
          description POM_DESCRIPTION
          url POM_URL

          scm {
            url POM_SCM_URL
            connection POM_SCM_CONNECTION
            developerConnection POM_SCM_DEV_CONNECTION
          }

          licenses {
            license {
              name POM_LICENCE_NAME
              url POM_LICENCE_URL
              distribution POM_LICENCE_DIST
            }
          }

          developers {
            developer {
              id POM_DEVELOPER_ID
              name POM_DEVELOPER_NAME
            }
          }
        }

        // Resolve dependencies to other modules
        pom.whenConfigured { pom ->
          pom.dependencies.findAll { dep -> dep.groupId == rootProject.name }.collect { dep ->
            dep.groupId = pom.groupId = project.GROUP
            dep.version = pom.version = project.VERSION_NAME
          }
        }
      }
    }

    task androidJavadocs(type: Javadoc) {
      source = android.sourceSets.main.java.source
      classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
      failOnError false
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
      classifier = 'javadoc'
      from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
      classifier = 'sources'
      from android.sourceSets.main.java.source
    }
  } else {
    install {
      repositories.mavenInstaller {
        pom.groupId = GROUP
        pom.artifactId = POM_ARTIFACT_ID
        pom.version = VERSION_NAME

        pom.project {
          name POM_NAME
          packaging POM_PACKAGING
          description POM_DESCRIPTION
          url POM_URL

          scm {
            url POM_SCM_URL
            connection POM_SCM_CONNECTION
            developerConnection POM_SCM_DEV_CONNECTION
          }

          licenses {
            license {
              name POM_LICENCE_NAME
              url POM_LICENCE_URL
              distribution POM_LICENCE_DIST
            }
          }

          developers {
            developer {
              id POM_DEVELOPER_ID
              name POM_DEVELOPER_NAME
            }
          }
        }

        // Resolve dependencies to other modules
        pom.whenConfigured { pom ->
          pom.dependencies.findAll { dep -> dep.groupId == rootProject.name }.collect { dep ->
            dep.groupId = pom.groupId = project.GROUP
            dep.version = pom.version = project.VERSION_NAME
          }
        }
      }
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
      classifier = 'sources'
      from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
      classifier = 'javadoc'
      from javadoc.destinationDir
    }
  }

  if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
  }

  artifacts {
    if (project.getPlugins().hasPlugin('com.android.application') ||
            project.getPlugins().hasPlugin('com.android.library')) {
      archives androidSourcesJar
      archives androidJavadocsJar
    } else {
      archives sourcesJar
      archives javadocJar
    }
  }
}