buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.mobilej.unmock:UnMockPlugin:0.7.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'de.mobilej.unmock'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        consumerProguardFiles 'proguard-rules.txt'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

configurations {
    lintChecks
}

dependencies {
    implementation rootProject.ext.supportAnnotations
    implementation rootProject.ext.supportV4

    testImplementation rootProject.ext.junit
    testImplementation rootProject.ext.robolectric

    api rootProject.ext.androidxAnnotations

    lintChecks project(path: ':conductor-lint', configuration: 'lintChecks')
    lintChecks project(path: ':conductor-modules:codegen-lint', configuration: 'lintChecks')

}

task copyLintJar(type: Copy) {
    from(configurations.lintChecks) {
        rename { 'lint.jar' }
    }
    into 'build/intermediates/lint/'
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}

ext.artifactId = 'conductor'

apply from: rootProject.file('dependencies.gradle')
apply from: rootProject.file('gradle-mvn-push.gradle')
